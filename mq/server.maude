load buffered-socket.maude
load queue.maude

mod SERVER is
    pr BUFFERED-SOCKET .
    pr IQUEUE{Job} .
    pr IQUEUE{Worker} .
    pr SET{Worker} .
    pr SOLVER-MESSAGE .
    pr SOLVER-MESSAGE-CONVERTER .

    op jobs :_ : Queue{Job} -> Attribute [ctor format(! o o o)] .
    op waitingWorkers :_ : Queue{Worker} -> Attribute [ctor format(! o o o)] .
    op workers :_ : Set{Worker} -> Attribute [ctor format(! o o o)] .

    vars J : Queue{Job} .
    vars W : Queue{Worker} .
    vars SW : Set{Worker} .

    op Server : -> Cid [ctor] .

    vars O O' O'' LISTENER CLIENT CACHE APP : Oid .
    var  A : AttributeSet .
    var  N : Nat .
    vars IP DATA S : String .
    vars C : Configuration .
    var ST : State .
    var SS : StateSet .

    crl [LookupReply] : < O : Server | jobs : J, nReq : N, A > lookupReply(O, O', ST)
    =>  < O : Server | jobs : (J | S), nReq : if N == 0 then N else sd(N, 1) fi, A >
    if ST =/= null
    /\ S := state2string(ST) [print "[LookupReply] State"] .

    rl [LookupReplyEmpty] : < O : Server | jobs : J, nReq : N, A > lookupReplyEmpty(O, O')
    =>  < O : Server | jobs : J, nReq : if N == 0 then N else sd(N, 1) fi, A > [print "[LookupReplyEmpty]"] .

    rl [LookupBatchReply] : < O : Server | jobs : J, A > lookupBatchReply(O, O', SS)
        => < O : Server | jobs : enqueueFromSet(SS, J), A > [print "[LookupBatchReply] States"] .

    op enqueueFromSet : StateSet Queue{Job} -> Queue{Job} .
    eq enqueueFromSet(empty, J) = J .
    eq enqueueFromSet((ST, SS), J) = enqueueFromSet(SS, J | state2string(ST)) .

    rl [StopClient] : < O : Server | workers : (CLIENT, SW), A > stopWorking(O, O')
        => CloseSocket(CLIENT, O) < O : Server | workers : SW, A > stopWorking(O, O') [print "[StopClient] " CLIENT] .

    rl [StopListener] : < O : Server | workers : empty, app : APP, cache : CACHE, listener : LISTENER, A > stopWorking(O, O')
        => < O : Server | none > CloseSocket(LISTENER, O) clearApp(APP, O) clearCache(CACHE, O) [print "[StopListener]"] .

    crl [CheckTermination] :
        < O : Server | workers : SW, jobs : J, waitingWorkers : W, nReq : N, A >
        => < O : Server | workers : SW, jobs : J, waitingWorkers : W, nReq : N, A > stopWorking(O, O)
        if N == 0
        /\ isEmpty(J)
        /\ qsize(W) > 0
        /\ qsize(W) == | SW | [print "[CheckTermination]"] .

    rl [MqCreatedSocket] :
        < O : Server | A > CreatedSocket(O, socketManager, LISTENER)
        => < O : Server | listener : LISTENER, workers : empty, jobs : emptyq, waitingWorkers : emptyq, nReq : 0, A >
        AcceptClient(LISTENER, O) [print "[MqCreatedSocket]"] .

    rl [MqAcceptedClient] :
        < O : Server | workers : SW, A > AcceptedClient(O, LISTENER, IP, CLIENT)
        => < O : Server | workers : (CLIENT, SW), A > Receive(CLIENT, O)
        AcceptClient(LISTENER, O) [print "[MqAcceptedClient]"] .

    crl [MqReceived] :
        < O : Server | jobs : J, waitingWorkers : W, A > Received(O, CLIENT, DATA)
        => < O : Server | jobs : J, waitingWorkers : (W | CLIENT), A >
        if DATA == getJob [print "[MqReceived] " DATA] .

    crl [MqReceived] :
        < O : Server | A > Received(O, CLIENT, DATA)
        => < O : Server | A > stopWorking(O, CLIENT)
        if DATA == stop [print "[MqReceived] " DATA] .

    rl [SendJob] : < O : Server | jobs : (S | J), waitingWorkers : (CLIENT | W), A >
        => < O : Server | jobs : J, waitingWorkers : W, A >
        Send(CLIENT, O, S) [print "[SendJob]"] .

    crl [MqReceived] :
        < O : Server | cache : CACHE, nReq : N, A > Received(O, CLIENT, DATA)
        => < O : Server | cache : CACHE, nReq : (N + 1), A > lookupReq(CACHE, O, ST)
        if DATA =/= getJob
        /\ DATA =/= stop
        /\ ST := string2state(DATA) [print "[MqReceived] State "] .

    rl [MqSent] :
        < O : Server | A > Sent(O, CLIENT)
        => < O : Server | A > [print "[MqSent]"] .

    rl [MqClosedSocket] :
        < O : Server | A > ClosedSocket(O, CLIENT, DATA)
        => < O : Server | A > [print "[MqClosedSocket]"] .
endm